set package.cpath = package.cpath ++ ';/usr/lib64/libiuplua54.so'
require <- "iuplua"

iup.timer <- [
    time = 100,
    run  = "YES",
    action_cb = func () {
        emit(:clock, 100)
    }
]

set iup.showxy = func (dlg, ...) {
    return(dlg.showxy(dlg, ...))
}

func iup_cb_action (h) {
    emit(:action [ h=h ])
}

func iup_cb_canvas_action (h, x, y) {
    emit(:canvas [ h=h, x=x, y=y ])
}

func iup_cb_value (h) {
    emit(:value [ h=h ])
}

val iup_button = iup.button
set iup.button = func (...) {
    val h = iup_button(...)
    set h.action = iup_cb_action
    return(h)
}

val iup_canvas = iup.canvas
set iup.canvas = func (...) {
    val h = iup_canvas(...)
    set h.action = iup_cb_canvas_action
    return(h)
}

val iup_list = iup.list
set iup.list = func (...) {
    val h = iup_list(...)
    set h.valuechanged_cb = iup_cb_value
    return(h)
}

val iup_text = iup.text
set iup.text = func (...) {
    val h = iup_text(...)
    set h.valuechanged_cb = iup_cb_value
    return(h)
}

set iup.main = func () {
    assert(iup.MainLoopLevel() == 0)
    iup.MainLoop()
    iup.Close()
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val lbl = iup.label <- [
    title = "label title",
]

val txt1 = iup.text <- [
    value    = "read-only text",
    readonly = 'YES',
    size     = 60,
]

val txt2 = iup.text <- [
    value = "default text",
    mask = "[+/-]?(/d+/.?/d*|/./d+)",
    size  = 60,
]

val lst = iup.list <- [
    "option 1", "option 2", "option 3",
    dropdown = "YES",
    value    = 2,
    expand   = "HORIZONTAL",
]

val gau = iup.gauge <- [
    min = 0,
    max = 100,
    ;;expand = "HORIZONTAL",
    ;;showtext = "NO",
]

val but = iup.button <- [
    title = "button title",
    size  = 60,
]

val cav = iup.canvas <- [
]

val out = iup.label <- [
    title = "label title",
]

val box = iup.vbox <- [
    ngap = 10,
    alignment = "acenter",
    lbl, txt1, txt2, lst, gau, but, cav, out,
]

val dlg = iup.dialog <- [
    title  = "dialog title",
    margin = "10x10",
    box
]

spawn {
    spawn {
        every @.100 {
            set gau.value = (gau.value + 1) % gau.max
        }
    }
    par {
        every (:action, evt.h==but) {
            set out.title = "button clicked"
        }
    } with {
        every (:value, evt.h==txt2) {
            set out.title = txt2.value
        }
    } with {
        every (:value, evt.h==lst) {
            set out.title = lst.value
        }
    }
}

iup.showxy(dlg, iup.CENTER, iup.CENTER)
iup.main()
